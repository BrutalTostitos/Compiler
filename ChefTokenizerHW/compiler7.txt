#define ordinal \b(1st|2nd|3rd|\d*[02-9]1st|\d*[02-9]2nd|\d*[02-9]3rd|\d*[4-9]th|\d*[1-9]\d*0th|\d*1[1-3]th)\b
TITLE_AND_COMMENT -> ^(
    (?!\s*Ingredients\.)(?!\s*Cooking\s+time\b)(?!\s*Pre-heat\s+oven\s+to\b)(?!\s*Method)
    [^.]+\.)
    (
        (?!\s*Ingredients\.)(?!\s*Cooking\s+time\b)(?!\s*Pre-heat\s+oven\s+to\b)(?!\s*Method)
        [^.]+\.
    )?
COOKING_TIME -> \bCooking\s+time\b
PUT_OR_FOLD -> \b(put|fold)\b
ADD_DRY_INGREDIENTS -> \badd\s+dry\s+ingredients
ADD -> \badd\b
STIR -> \bstir\b
MIX -> \bmix\b
REMOVE -> \bremove\b
COMBINE -> \bcombine\b
DIVIDE -> \bdivide\b
CLEAN_MIXING_BOWL -> \bclean\s+mixing\s+bowl\b
CLEAN_NTH_MIXING_BOWL -> \bclean\s+(?#ordinal)\s+mixing\s+bowl\b
TO_MIXING_BOWL -> \bto\s+mixing\s+bowl\b
TO_NTH_MIXING_BOWL -> \bto\s+(?#ordinal)\s+mixing\s+bowl\b
FROM_MIXING_BOWL -> \bfrom\s+mixing\s+bowl\b
FROM_NTH_MIXING_BOWL -> \bfrom\s+(?#ordinal)\s+mixing\s+bowl\b
INTO_MIXING_BOWL -> \binto\s+mixing\s+bowl\b
INTO_NTH_MIXING_BOWL -> \binto\s+(?#ordinal)\s+mixing\s+bowl\b
THE_MIXING_BOWL -> \bthe\s+mixing\s+bowl\b
THE_NTH_MIXING_BOWL -> \bthe\s+(?#ordinal)\s+mixing\s+bowl\b
INGREDIENTS -> \bIngredients\b
PREHEAT_OVEN_TO -> \bPre-heat\s+oven\s+to\b
NUM -> \b(\d+/\d+|\d+-\d+/\d+|\d+)\b
TIMEUNIT -> \b(hours|minute(s)?|seconds)\b
DEGREES -> \bdegrees\b
GAS_MARK -> \bgas\s+mark\b
THERMOSTAT -> \b(thermostat|Th)\b
STUFE -> \bStufe\b
SERVES -> \bServes\b
METHOD -> \bMethod\b
DESCRIPTIVE -> \b(heaped|heaping|packed|level)\b
MEASURE -> \b(oz|lb|pinch|pinches|quart|quarts|dash|dashes|cup|cups|teaspoon|teaspoons|tablespoon|tablespoons|tsp|T|t|tbsp)\b
WORD -> \b([a-z]+)\b
COLON -> :
PERIOD -> \.

S -> TITLE_AND_COMMENT 
    optionalIngredients 
    optionalCookingTime 
    optionalPreheat 
    method 
    optionalServes
optionalIngredients -> INGREDIENTS PERIOD ingredientList | lambda
ingredientList -> ingredient ingredientList | lambda
ingredient -> optionalInitialValue optionalMeasureType wordList PERIOD
optionalMeasureType -> optionalDescriptive MEASURE | lambda
optionalDescriptive -> DESCRIPTIVE | lambda
optionalInitialValue -> NUM | lambda
optionalCookingTime -> COOKING_TIME COLON NUM TIMEUNIT PERIOD | lambda
optionalPreheat -> PREHEAT_OVEN_TO tempSpec PERIOD | lambda
tempSpec -> NUM DEGREES | GAS_MARK NUM | THERMOSTAT NUM | STUFE NUM
method -> METHOD PERIOD stmt-list
stmt-list -> stmt PERIOD stmt-list | lambda
stmt -> putStmt | cleanStmt | mathStmt | stirStmt | mixStmt
putStmt -> PUT_OR_FOLD wordList INTO_MIXING_BOWL |
           PUT_OR_FOLD wordList INTO_NTH_MIXING_BOWL
cleanStmt -> CLEAN_MIXING_BOWL |
             CLEAN_NTH_MIXING_BOWL
mathStmt -> addStmt | subStmt | mulStmt | divStmt
addStmt -> ADD wordList | ADD wordList TO_MIXING_BOWL | ADD wordList TO_NTH_MIXING_BOWL |
           ADD_DRY_INGREDIENTS | ADD_DRY_INGREDIENTS TO_MIXING_BOWL |
           ADD_DRY_INGREDIENTS TO_NTH_MIXING_BOWL 
stirStmt -> STIR wordList NUM TIMEUNIT | STIR THE_NTH_MIXING_BOWL wordList NUM TIMEUNIT | STIR THE_MIXING_BOWL wordList NUM TIMEUNIT| STIR wordList THE_MIXING_BOWL |STIR wordList THE_NTH_MIXING_BOWL
mixStmt -> MIX wordList | MIX THE_MIXING_BOWL wordList | MIX THE_NTH_MIXING_BOWL wordList
subStmt -> REMOVE wordList | REMOVE wordList FROM_MIXING_BOWL | REMOVE wordList FROM_NTH_MIXING_BOWL
mulStmt -> COMBINE wordList | COMBINE wordList INTO_MIXING_BOWL | COMBINE wordList INTO_NTH_MIXING_BOWL
divStmt -> DIVIDE wordList | DIVIDE wordList INTO_MIXING_BOWL | DIVIDE wordList INTO_NTH_MIXING_BOWL
optionalServes -> SERVES NUM PERIOD | lambda
wordList -> WORD wordList | WORD
